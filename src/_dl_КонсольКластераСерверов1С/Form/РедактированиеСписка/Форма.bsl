 
 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Если Параметры.Свойство("Перечисление") Тогда
		Элементы.СписокФлаг.Видимость = Истина;
		Элементы.СписокЗначение.ТолькоПросмотр = Истина;
		
		ПеречислениеЗначений 	= Параметры.Перечисление;
		МассивПеречисления 		= ПолучитьПеречислениеИзМакета(ПеречислениеЗначений); 
		
		Для каждого Элемент Из МассивПеречисления Цикл
			СтрокаТЧ 			= Список.Добавить();    
			СтрокаТЧ.Значение 	= Элемент;
		КонецЦикла;
	КонецЕсли;     
	
	Если Параметры.Свойство("Значение") Тогда
		Если Параметры.Значение <> "" Тогда
			МассивЗначений = СтрРазделить(Параметры.Значение, ";", Ложь);
			Для каждого Элемент Из МассивЗначений Цикл 
				Если ПеречислениеЗначений <> "" Тогда
					НайдСтроки = Список.НайтиСтроки(Новый Структура("Значение", Элемент));
					
					Для каждого СтрокаТЧ Из НайдСтроки Цикл
						СтрокаТЧ.Флаг = Истина;	
					КонецЦикла; 
					Продолжить;
				КонецЕсли;
				
				СтрокаТЧ 			= Список.Добавить();    
				СтрокаТЧ.Значение 	= Элемент;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли; 
	
	Если Параметры.Свойство("Маска") Тогда
		Элементы.СписокЗначение.Маска = Параметры.Маска;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Ок(Команда)
	
	Массив = Новый Массив;
	Для каждого СтрокаТЧ Из Список Цикл  
		Если ПеречислениеЗначений <> "" И Не СтрокаТЧ.Флаг Тогда
			Продолжить;
		КонецЕсли;
		
		Массив.Добавить(СтрокаТЧ.Значение);
	КонецЦикла;
	
	СтрокаРезультат = СтрСоединить(Массив, ";");
	
	ОповеститьОВыборе(СтрокаРезультат);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПеречислениеИзМакета(ИмяОбласти) Экспорт
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Макет 			= ОбработкаОбъект.ПолучитьМакет("ПеречисленияЗначений");
	
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	
	МассивПеречисления = Новый Массив;
	
	Для ИндСтроки = 1 По Область.ВысотаТаблицы Цикл          
		МассивПеречисления.Добавить(Область.Область(ИндСтроки, 2, ИндСтроки, 2).Текст);
	КонецЦикла;
	 
	Возврат МассивПеречисления; 
	
КонецФункции

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если ПеречислениеЗначений <> "" Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Если ПеречислениеЗначений <> "" Тогда
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры
